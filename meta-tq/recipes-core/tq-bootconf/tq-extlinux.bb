SUMMARY = "Minimal extlinux.conf to boot TQ-Systems distributions"
LICENSE = "MIT"

# Enable extlinux.conf generation in uboot-extlinux-config.bbclass
UBOOT_EXTLINUX = "1"
# Instead of specifying a static root= argument, we expect the bootloader to pass one
# in bootargs_root.
UBOOT_EXTLINUX_ROOT ?= "${bootargs_root}"

inherit uboot-config uboot-extlinux-config deploy

# The file contents are generated by uboot-extlinux-config
# We intentionally don't set a FDT filename, so the bootloader can choose the
# correct variant based on the hardware, or the default can be overridden
# in the environment to enabled advanced features.

do_install () {
    install -Dm 0644 ${UBOOT_EXTLINUX_CONFIG} ${D}/${UBOOT_EXTLINUX_INSTALL_DIR}/${UBOOT_EXTLINUX_CONF_NAME}
}
FILES:${PN} = "${UBOOT_EXTLINUX_INSTALL_DIR}/${UBOOT_EXTLINUX_CONF_NAME}"

do_deploy () {
    install -m 644 ${UBOOT_EXTLINUX_CONFIG} ${DEPLOYDIR}/${UBOOT_EXTLINUX_SYMLINK}
    ln -sf ${UBOOT_EXTLINUX_SYMLINK} ${DEPLOYDIR}/${UBOOT_EXTLINUX_CONF_NAME}-${MACHINE}
    ln -sf ${UBOOT_EXTLINUX_SYMLINK} ${DEPLOYDIR}/${UBOOT_EXTLINUX_CONF_NAME}
}
addtask deploy before do_build after do_compile

PACKAGE_ARCH = "${MACHINE_ARCH}"
