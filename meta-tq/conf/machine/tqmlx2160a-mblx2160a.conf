#@TYPE: Machine
#@NAME: TQ-Systems GmbH TQMLX2160A Module on MBLX2160A Baseboard
#@SOC: lx2160A
#@DESCRIPTION: Machine configuration for TQ-Systems GmbH TQMLX2160A
#@MAINTAINER: Gregor Herburger <oss@ew.tq-group.com>
#
require conf/machine/include/tqmlx2160a.inc

# no default assignment here, since we overwrite defaults from qoriq-arm64.inc
SERIAL_CONSOLES = "115200;ttyAMA0"
SERIAL_CONSOLES_CHECK = "${SERIAL_CONSOLES}"
USE_VT = "0"

KERNEL_DEVICETREE ?= " \
    freescale/fsl-lx2160a-mblx2160a.dtb \
"

IMAGE_BOOT_FILES = "\
    mc_app/mc.itb;mc.itb \
    mc-utils/${MC_DPC};${MC_DPC} \
    mc-utils/${MC_DPL};${MC_DPL} \
"

# Add packages we want to be deployed, even when they are not part of rootfs
# We need these here to support UBI / NFS boot case
EXTRA_IMAGEDEPENDS += "\
    virtual/bootloader \
    virtual/kernel \
"

# direct assignment to override empty default from meta-freescale
WKS_FILE = "tqmlx2160a.wks.in"

# EXTRA_IMAGEDEPENDS are applied to do_image_complete only. do_image_wic may run
# before and needs those packages as well
WKS_FILE_DEPENDS += "${EXTRA_IMAGEDEPENDS}"

UBOOT_CONFIG ?= "tfa"

UBOOT_CONFIG[tfa] = "tqmlx2160a_mblx2160a_tfa_defconfig,wic"

# auto boottype can boot from both eMMC and SD card
BOOTTYPE ?= "\
    auto \
    flexspi_nor \
"

# Reset Configuration Word for ATF
RCWXSPI ?= "${MACHINE}/rcw_2200_750_2800_14_11_2"
RCWAUTO ?= "${MACHINE}/rcw_2200_750_2800_14_11_2"
ATF_RCW_VARIANTS ?= " \
    ${MACHINE}/rcw_2200_750_2800_0_0_0 \
    ${MACHINE}/rcw_2200_750_2800_12_11_3 \
    ${MACHINE}/rcw_2200_750_2800_12_7_3 \
    ${MACHINE}/rcw_2200_750_2800_12_8_3 \
    ${MACHINE}/rcw_2200_750_2800_14_11_2 \
    ${MACHINE}/rcw_2200_750_2800_14_11_3 \
    ${MACHINE}/rcw_2200_750_2800_14_7_2 \
    ${MACHINE}/rcw_2200_750_2800_14_7_3 \
    ${MACHINE}/rcw_2200_750_2800_14_8_2 \
    ${MACHINE}/rcw_2200_750_2800_14_8_3 \
"

# Build additional bootonly image with this PBL variant
PBL_VARIANT = "bl2_auto_tqmlx2160a_16gb"

IMAGE_OFFSET_PBL ?= "4"
IMAGE_OFFSET_FIP ?= "1024"
IMAGE_MAXSIZE_PBL ?= "1020"

#
# mc-utils example configuration for starter kit
# needs to be adjusted for custom boards / setups
#
MC_FOLDER ?= "${MACHINE}"
MC_DPC ?= "dpc-warn.dtb"
MC_DPL ?= "dpl-min.dtb"

# Do not put Images into /boot of rootfs, install kernel-image if needed
RRECOMMENDS:${KERNEL_PACKAGE_NAME}-base = ""
