#@TYPE: Machine
#@NAME: TQ-Systems GmbH TQMLX2160A Module on MBLX2160A Baseboard
#@SOC: lx2160A
#@DESCRIPTION: Machine configuration for TQ-Systems GmbH TQMLX2160A
#@MAINTAINER: Gregor Herburger <gregor.herburger@tq-group.com>
#
require conf/machine/include/tqmlx2160a.inc

# no default assignment here, since we overwrite defaults from qoriq-arm64.inc
SERIAL_CONSOLES = "115200;ttyS1"
SERIAL_CONSOLES_CHECK = "${SERIAL_CONSOLES}"
USE_VT = "0"

KERNEL_DEVICETREE ?= " \
    freescale/fsl-lx2160a-mblx2160a.dtb \
"

# Files are needed in IMAGE_BOOT_FILES because /boot is on an extra partition.
# If those files should be installed under /boot inside the rootfs, you can
# achieve this in your BSP layer adding them via
# IMAGE_INSTALL:append = " kernel-devicetree kernel-image-zimage"
#
IMAGE_BOOT_FILES = "\
    ${KERNEL_IMAGETYPE} \
    fsl-lx2160a-mblx2160a.dtb \
    mc_app/mc.itb;mc.itb \
    mc-utils/${MC_DPC};${MC_DPC} \
    mc-utils/${MC_DPL};${MC_DPL} \
"

# Add packages we want to be deployed, even when they are not part of rootfs
# We need these here to support UBI / NFS boot case
EXTRA_IMAGEDEPENDS += "\
    virtual/bootloader \
    virtual/kernel \
"

# direct assignment to override empty default from meta-freescale
WKS_FILE = "tqmlx2160a.wks"

# EXTRA_IMAGEDEPENDS are applied to do_image_complete only. do_image_wic may run
# before and needs those packages as well
WKS_FILE_DEPENDS += "${EXTRA_IMAGEDEPENDS}"

# Do not update fstab file when using wic images
WIC_CREATE_EXTRA_ARGS = "--no-fstab-update"

UBOOT_CONFIG ?= "tfa"

UBOOT_CONFIG[tfa] = "tqmlx2160a_mblx2160a_tfa_defconfig,wic"

# Reset Configuration Word for ATF
RCWXSPI ?= "${MACHINE}/rcw_2200_750_2800_14_11_2"
RCWSD ?= "${MACHINE}/rcw_2200_750_2800_14_11_2"
RCWEMMC ?= "${MACHINE}/rcw_2200_750_2800_14_11_2"
ATF_RCW_VARIANTS ?= " \
    ${MACHINE}/rcw_2200_750_2800_0_0_0.bin \
    ${MACHINE}/rcw_2200_750_2800_12_11_3.bin \
    ${MACHINE}/rcw_2200_750_2800_12_7_3.bin \
    ${MACHINE}/rcw_2200_750_2800_12_8_3.bin \
    ${MACHINE}/rcw_2200_750_2800_14_11_2.bin \
    ${MACHINE}/rcw_2200_750_2800_14_11_3.bin \
    ${MACHINE}/rcw_2200_750_2800_14_7_2.bin \
    ${MACHINE}/rcw_2200_750_2800_14_7_3.bin \
    ${MACHINE}/rcw_2200_750_2800_14_8_2.bin \
    ${MACHINE}/rcw_2200_750_2800_14_8_3.bin \
"

#
# mc-utils example configuration for starter kit
# needs to be adjusted for custom boards / setups
#
MC_FOLDER ?= "${MACHINE}"
MC_DPC ?= "dpc-warn.dtb"
MC_DPL ?= "dpl-min.dtb"

BOOTTYPE ?= "\
    emmc \
    flexspi_nor \
    sd \
"

# Do not put Images into /boot of rootfs, install kernel-image if needed
RRECOMMENDS:${KERNEL_PACKAGE_NAME}-base = ""
