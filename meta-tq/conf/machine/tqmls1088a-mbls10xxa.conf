#@TYPE: Machine
#@NAME: TQ-Systems GmbH TQMLS1088A Module on MBLS10xxa Baseboard
#@SOC: ls1088a
#@DESCRIPTION: Machine configuration for TQ-Systems GmbH TQMLS1088a
#@MAINTAINER: Timo Herbrecher <t.herbrecher@gateware.de>
#
# By default, this machine will build for the full featured option
# with QSPI-NOR boot
#
# To build U-Boot for other CPU or memory combinations, you can set
# the UBOOT_MACHINE variable in your local.conf according to the
# following table:
#
# Processor Memory          Boot Device  Configuration
# --------- ---------------	------------ --------------------
# LS1088A   2048MiB / ECC   QSPI-NOR     tqmls1088a_mbls10xxa_qspi_defconfig
# LS1088A   2048MiB / ECC   SD / eMMC    tqmls1088a_mbls10xxa_esdhc_defconfig
#

require conf/machine/include/tqmls1088a.inc

# no default assignment here, since we overwrite defaults from qoriq-arm64.inc
SERIAL_CONSOLES = "115200;ttyS1"
SERIAL_CONSOLES_CHECK = "${SERIAL_CONSOLES}"
# disable virtual console support for this board and limit getty to SERIAL_CONSOLES only
USE_VT = "0"

KERNEL_DEVICETREE ?= " \
    freescale/fsl-tqmls1088a-mbls10xxa.dtb \
"

# Files are needed in IMAGE_BOOT_FILES because /boot is on an extra partition.
# If those files should be installed under /boot inside the rootfs, you can
# achieve this in your BSP layer adding them via
# IMAGE_INSTALL:append = " kernel-devicetree kernel-image-zimage"
#
IMAGE_BOOT_FILES = "\
    ${KERNEL_IMAGETYPE};linuximage \
    fsl-tqmls1088a-mbls10xxa.dtb \
"

# direct assignment to override empty default from meta-freescale
WKS_FILE = "tqmls1088a-sdimg.wks.in"

WKS_FILE_DEPENDS += "\
    dosfstools-native \
    e2fsprogs-native \
    mtools-native \
    parted-native \
"

# Install kernel and devicetree also under /boot inside the rootfs,
# needed for UBIFS use case to prevent extra partition(s) for kernel and dtb.
MACHINE_EXTRA_RRECOMMENDS += "\
    kernel-devicetree \
    kernel-image \
"

WICVARS:append = " RCWIMG MC_DPL MC_DPC"

RCWIMG ?= "tqmls1088a-mbls10xxa/${@d.getVar('RCW' + d.getVar('BOOTMODE').upper())}"

UBOOT_CONFIG ??= "qspi esdhc"
UBOOT_CONFIG[qspi] = "tqmls1088a_mbls10xxa_qspi_defconfig,ubi"
UBOOT_CONFIG[esdhc] = "tqmls1088a_mbls10xxa_esdhc_defconfig,wic"

# Reset Configuration Word for TQMLS1088A is handled by NXP RCW tool (see fork
# in TQ rcw git repository). Following variables select the RCW files used for
# different boot sources.
RCWSD ??= "RR_SSSSPPPH_3333_5559/rcw_1400_sdboot.bin"
RCWEMMC ??= "RR_SSSSPPPH_3333_5559/rcw_1400_emmcboot.bin"
RCWQSPI ??= "RR_SSSSPPPH_3333_5559/rcw_1400_qspiboot.bin"

#
# mc-utils example configuration for starter kit
# needs to be adjusted for custom boards / setups
#
MC_FOLDER ?= "${MACHINE}"
MC_DPC ?= "dpc.0x12_0x0D.dtb"
MC_DPL ?= "dpl-eth.0x12_0x0D.dtb"
