#@TYPE: Machine
#@NAME: TQ-Systems GmbH TQMa6ULxL SOM on MBa6ULxL baseboard
#@SOC: i.MX6UL
#@DESCRIPTION: Machine configuration for TQ-Systems GmbH TQMaULxL on MBa6ULxL
#@MAINTAINER: Markus Niebel <Markus.Niebel@tq-group.com>
#
# By default, this machine be will build for the full featured option
# with SD / eMMC boot
#
# To build U-Boot for other CPU or memory combinations, you can set
# the UBOOT_MACHINE variable in your local.conf according to the
# following table:
#
# Processor     Memory     Boot device     Configuration
# ----------    -------    ------------    ---------------------------------
# i.MX6UL       256MiB     SD / e-MMC      tqma6ulxl_mba6ulxl_mmc_defconfig
#

include conf/machine/include/tqma6ul.inc

# no sound on MBa6ULxL
MACHINE_FEATURES:remove = "alsa"

SERIAL_CONSOLES = "115200;ttymxc0"

KERNEL_DEVICETREE_linux-imx-tq = "\
    imx6ul-mba6ulxl.dtb \
    imx6ul-mba6ulxl-glyn-etm0700g0edh6.dtb \
"

KERNEL_DEVICETREE_linux-tq = "\
    imx6ul-tqma6ul2l-mba6ulxl.dtb \
    imx6ul-tqma6ul2l-mba6ulxl-glyn-etm0700g0edh6.dtb \
"

KERNEL_DEVICETREE += " ${@kernel_provider_dtbs(d)}"

# Files are needed in IMAGE_BOOT_FILES because we use an extra partition for
# backward compatibility.
IMAGE_BOOT_FILES = "\
    ${KERNEL_IMAGETYPE};linuximage \
    ${KERNEL_DEVICETREE} \
"

MACHINE_FEATURES += "\
    bluetooth \
    wifi \
"

# install kernel and devicetree also under /boot inside the rootfs,
# needed for UBIFS use case and perpare removal of extra FAT boot partition
# on SD / e-MMC
MACHINE_EXTRA_RRECOMMENDS += "\
    kernel-devicetree \
    kernel-image \
    linux-firmware-ath10k \
    linux-firmware-qca \
"

WKS_FILE = "tqmaxx-imx-v7-sdimg.wks.in"

# allow to easy override for different configuration
MACHINE_WKS_BOOTSTREAM ?= "u-boot-${MACHINE}.imx-sd"

# EXTRA_IMAGEDEPENDS are applied to do_image_complete only. do_image_wic may run
# before and needs those packages as well
WKS_FILE_DEPENDS += "${EXTRA_IMAGEDEPENDS}"

UBOOT_CONFIG ?= "sd"
UBOOT_CONFIG[sd] = "tqma6ulxl_mba6ulxl_mmc_defconfig,wic"
