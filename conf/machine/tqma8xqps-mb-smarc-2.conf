#@TYPE: Machine
#@NAME: i.MX 8QXP TQMa8XQPS on MB-SMARC-2
#@DESCRIPTION: i.MX8QXP MB-SMARC-2 kit
#@MAINTAINER: Markus Niebel <Markus.Niebel@tq-group.com>
#
# Processor       Boot device              Memory    Configuration
# -----------     -----------              ------    -----------------------------
# i.MX8QXP C0     e-MMM / SD               2GiB      tqma8xqps_mb_smarc_2_mmc_defconfig
# i.MX8QXP C0     FlexSPI                  2GiB      tqma8xqps_mb_smarc_2_fspi_defconfig

require conf/machine/include/tqma8xxs.inc
require conf/machine/include/mb-smarc-2-common.inc

# TODO:
BOARD_DEVICETREE_FILES = "\
	imx8qxp-mb-smarc-2.dtb \
	imx8qxp-mb-smarc-2-lvds-tm070jvhg33.dtb \
"

require conf/machine/include/tqma8-common.inc

# Files are needed in IMAGE_BOOT_FILES because /boot is on an extra partition.
# If those files should be installed under /boot inside the rootfs, you can
# achieve this in your BSP layer adding them via
# IMAGE_INSTALL_append = " kernel-devicetree kernel-image-zimage"
#
IMAGE_BOOT_FILES ?= "\
	${KERNEL_IMAGETYPE} \
	${BOARD_DEVICETREE_FILES} \
	"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "tqma8xqps_mb_smarc_2_mmc_defconfig,wic"
UBOOT_CONFIG[fspi] = "tqma8xqps_mb_smarc_2_fspi_defconfig"

# TODO: handle optee as soon as supported
IMXBOOT_TARGETS_SD = "flash_spl"
IMXBOOT_TARGETS_FSPI = "flash_flexspi"
IMXBOOT_TARGETS = \
    "${@bb.utils.contains('UBOOT_CONFIG', 'sd', '${IMXBOOT_TARGETS_SD}', \
                                                '${IMXBOOT_TARGETS_FSPI}', d)}"
