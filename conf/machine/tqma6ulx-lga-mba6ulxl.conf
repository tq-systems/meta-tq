#@TYPE: Machine
#@NAME: TQ-Systems GmbH TQMa6ULxL SOM on MBa6ULxL baseboard
#@SOC: i.MX6UL
#@DESCRIPTION: Machine configuration for TQ-Systems GmbH TQMaULxL on MBa6ULxL
#@MAINTAINER: Markus Niebel <Markus.Niebel@tq-group.com>
#
# By default, this machine be will build for the full featured option
# with SD / eMMC boot
#
# To build U-Boot for other CPU or memory combinations, you can set
# the UBOOT_MACHINE variable in your local.conf according to the
# following table:
#
# Processor     Memory     Boot device     Configuration
# ----------    -------    ------------    ---------------------------------
# i.MX6UL       256MiB     SD / e-MMC      tqma6ulxl_mba6ulxl_mmc_defconfig
#

include conf/machine/include/tqma6ul.inc

# no sound on MBa6ULxL
MACHINE_FEATURES_remove = "alsa"

SERIAL_CONSOLES = "115200;ttymxc0"

KERNEL_DEVICETREE_linux-imx-tq = "\
    imx6ul-mba6ulxl.dtb \
    imx6ul-mba6ulxl-glyn-etm0700g0edh6.dtb \
"

KERNEL_DEVICETREE_linux-tq = "\
    imx6ul-tqma6ul2l-mba6ulxl.dtb \
    imx6ul-tqma6ul2l-mba6ulxl-glyn-etm0700g0edh6.dtb \
"

KERNEL_DEVICETREE_append = " ${@kernel_provider_dtbs(d)} "

# Files are needed in IMAGE_BOOT_FILES because /boot is on an extra partition.
# If those files should be installed under /boot inside the rootfs, you can
# achieve this in your BSP layer adding them via
# IMAGE_INSTALL_append = " kernel-devicetree kernel-image-zimage"
#
IMAGE_BOOT_FILES = "\
    ${KERNEL_IMAGETYPE};linuximage \
    ${KERNEL_DEVICETREE} \
"

MACHINE_FEATURES_append = "\
    bluetooth \
    wifi \
"

MACHINE_EXTRA_RRECOMMENDS_append = "\
    linux-firmware-ath10k \
    linux-firmware-qca \
"

WKS_FILE = "tqmaxx-sdimg.wks"

UBOOT_CONFIG ??= "sd"
UBOOT_CONFIG[sd] = "tqma6ulxl_mba6ulxl_mmc_defconfig,wic"
