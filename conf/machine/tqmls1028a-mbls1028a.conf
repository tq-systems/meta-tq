#@TYPE: Machine
#@NAME: TQ-Group TQMLS1028a Module on MBLS1028a Baseboard
#@SOC: ls1028a
#@DESCRIPTION: Machine configuration for TQ-Group TQMLS1028a
#@MAINTAINER: Matthias Schiffer <matthias.schiffer@tq-group.com>
#
# By default, this machine will build for the full featured option
# with SD / eMMC boot
#
# To build U-Boot for other CPU or memory combinations, you can set
# the UBOOT_MACHINE variable in your local.conf according to the
# following table:
#
# Processor	Boot Device	Configuration
# --------- 	------------	-----------------------------------
# LS1028A	SD		tqmls1028a_mbls1028a_sdcard_defconfig
# LS1028A	eMMC		tqmls1028a_mbls1028a_emmc_defconfig

# Note: This file is included by tqmls1028a-mbls1028a-ind.conf.
# Modifications to this file may require adjustments to the -ind
# configuration as well.

require conf/machine/include/tqmls1028a.inc

SERIAL_CONSOLES ?= "115200;ttyS1"
SERIAL_CONSOLES_CHECK ?= "${SERIAL_CONSOLES}"
USE_VT ?= "0"

KERNEL_DEVICETREE ?= " \
	freescale/fsl-ls1028a-mbls1028a.dtb \
	freescale/fsl-ls1028a-mbls1028a-ind.dtb \
"

# Files are needed in IMAGE_BOOT_FILES because /boot is on an extra partition.
# If those files should be installed under /boot inside the rootfs, you can
# achieve this in your BSP layer adding them via
# IMAGE_INSTALL_append = " kernel-devicetree kernel-image-zimage"
#
IMAGE_BOOT_FILES ?= "\
	${KERNEL_IMAGETYPE} \
	fsl-ls1028a-mbls1028a.dtb \
	fsl-ls1028a-mbls1028a-ind.dtb \
"
IMAGE_BOOT_FILES_append_qoriq = " \
	dp/ls1028a-dp-fw.bin;ls1028a-dp-fw.bin \
"

UBOOT_CONFIG ??= "sd emmc"

UBOOT_CONFIG[sd] = "tqmls1028a_mbls1028a_sdcard_defconfig,wic"
UBOOT_CONFIG[emmc] = "tqmls1028a_mbls1028a_emmc_defconfig,wic"

# Reset Configuration Word for TQMLS1028A is handled by NXP RCW tool (see fork
# in TQ rcw git repository). Following variables select the RCW files used for
# different boot sources.
RCWSD ?= "tqmls1028a/rcw_1300_sd.bin"
RCWEMMC ?= "tqmls1028a/rcw_1300_emmc.bin"
RCWQSPI ?= "tqmls1028a/rcw_1300_spi_nor.bin"
