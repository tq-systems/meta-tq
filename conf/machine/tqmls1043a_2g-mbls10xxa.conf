#@TYPE: Machine
#@NAME: TQ-Systems GmbH TQMLS1043A Module on MBLS10xxa Baseboard
#@SOC: ls1043a
#@DESCRIPTION: Machine configuration for TQ-Systems GmbH TQMLS1043A
#@MAINTAINER: Gregor Herburger <gregor.herburger@tq-group.com>
#
# By default, this machine will build for the full featured option
# with QSPI-NOR boot
#
# To build U-Boot for other CPU or memory combinations, you can set
# the UBOOT_MACHINE variable in your local.conf according to the
# following table:
#
# Processor Memory          Boot Device  Configuration
# --------- ---------------	------------ --------------------
# LS1043A   2048MiB / ECC   QSPI-NOR     tqmls1043a_mbls10xxa_qspi_2g_defconfig
# LS1043A   2048MiB / ECC   SD / eMMC    tqmls1043a_mbls10xxa_esdhc_2g_defconfig
#
# The reset-configuration word (RCW) for the board is included in the
# u-boot source code. There are various RCWs supported for different
# SerDes configurations supported on the MBLS10xxA baseboard. Select
# RCW by setting UBOOT_RCW_CONFIG variable in your local.conf to one
# of the following values, otherwise u-boot default would be used:
# - TQMLS1043A_RCW_3358

require conf/machine/include/tqmls1043a.inc

SERIAL_CONSOLES ?= "115200;ttyS1"
SERIAL_CONSOLES_CHECK ?= "${SERIAL_CONSOLES}"
# disable virtual console support for this board and limit getty to SERIAL_CONSOLES only
USE_VT = "0"

KERNEL_DEVICETREE ?= " \
    freescale/fsl-tqmls1043a-mbls10xxa.dtb \
"

# Files are needed in IMAGE_BOOT_FILES because /boot is on an extra partition.
# If those files should be installed under /boot inside the rootfs, you can
# achieve this in your BSP layer adding them via
# IMAGE_INSTALL_append = " kernel-devicetree kernel-image-zimage"
#
IMAGE_BOOT_FILES = "\
    ${KERNEL_IMAGETYPE};linuximage \
    fsl-tqmls1043a-mbls10xxa.dtb \
"

UBOOT_CONFIG ??= "qspi esdhc"
UBOOT_CONFIG[qspi] = "tqmls1043a_mbls10xxa_qspi_2g_defconfig,ubi"
UBOOT_CONFIG[esdhc] = "tqmls1043a_mbls10xxa_esdhc_2g_defconfig,wic"

UBOOT_RCW_CONFIG ??= ""
