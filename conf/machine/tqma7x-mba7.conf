#@TYPE: Machine
#@NAME: TQ-Group TQMa7x Module on MBa7 Baseboard
#@SOC: i.MX7
#@DESCRIPTION: Machine configuration for TQ-Group TQMa7 SOLO / DUAL
#@MAINTAINER: Markus Niebel <Markus.Niebel@tq-group.com>
#
# By default, this machine will build for the full featured option
# with SD / eMMC boot
#
# To build U-Boot for other CPU or memory combinations, you can set
# the UBOOT_MACHINE variable in your local.conf according to the
# following table:
#
# Processor           Memory  Boot device  Configuration
# --------------      ------  -----------  --------------------
# i.MX7 S/D	      512MiB  MMC / SD     tqma7d_512mb_mba7_mmc_defconfig
# i.MX7 S/D	      512MiB  QSPI NOR     tqma7d_512mb_mba7_qspi_defconfig
# i.MX7 S/D	      1GiB    MMC / SD     tqma7d_1gb_mba7_mmc_defconfig
# i.MX7 S/D	      1GiB    QSPI NOR     tqma7d_1gb_mba7_qspi_defconfig
# i.MX7 S/D	      2GiB    MMC / SD     tqma7d_2gb_mba7_mmc_defconfig
# i.MX7 S/D	      2GiB    QSPI NOR     tqma7d_2gb_mba7_qspi_defconfig
#

include conf/machine/include/tqma7.inc

SERIAL_CONSOLES = "115200;ttymxc5"

KERNEL_DEVICETREE = " \
	imx7d-mba7.dtb \
	imx7d-mba7-lvds.dtb \
	imx7d-mba7-dmb-ct21.dtb \
	imx7d-mba7-dmb-ct44.dtb \
	imx7d-mba7-glyn-etm0700g0edh6.dtb \
"

#####
# TODO: TQMa7S does not work at the moment
#	imx7s-mba7.dtb \
#	imx7s-mba7-lvds.dtb \
#	imx7s-mba7-dmb-ct21.dtb \
#	imx7s-mba7-dmb-ct44.dtb \
#	imx7s-mba7-glyn-etm0700g0edh6.dtb \
####

# Files are needed in IMAGE_BOOT_FILES because /boot is on an extra partition.
# If those files should be installed under /boot inside the rootfs, you can
# achieve this in your BSP layer adding them via
# IMAGE_INSTALL_append = " kernel-devicetree kernel-image-zimage"
#
IMAGE_BOOT_FILES = "\
	${KERNEL_IMAGETYPE};linuximage \
	${KERNEL_DEVICETREE} \
"

WKS_FILE ?= "tqmaxx-sdimg.wks"

UBOOT_CONFIG ??= "sd sd_512mb sd_1gb sd_2gb qspi_512mb qspi_1gb qspi_2gb"
UBOOT_CONFIG[sd] = "tqma7d_512mb_mba7_mmc_defconfig"
UBOOT_CONFIG[sd_512mb] = "tqma7d_512mb_mba7_mmc_defconfig,wic"
UBOOT_CONFIG[sd_1gb] = "tqma7d_1gb_mba7_mmc_defconfig"
UBOOT_CONFIG[sd_2gb] = "tqma7d_2gb_mba7_mmc_defconfig"
UBOOT_CONFIG[qspi_512mb] = "tqma7d_512mb_mba7_qspi_defconfig,ubi"
UBOOT_CONFIG[qspi_1gb] = "tqma7d_1gb_mba7_qspi_defconfig"
UBOOT_CONFIG[qspi_2gb] = "tqma7d_2gb_mba7_qspi_defconfig"
# not supported in this version:
# UBOOT_CONFIG[mfgtool] = "tqma7d_512mb_mba7_mmc_defconfig"
