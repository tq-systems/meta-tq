#@TYPE: Machine
#@NAME: TQ-Systems GmbH TQMLX2160A Module on MBLX2160A Baseboard
#@SOC: lx2160A
#@DESCRIPTION: Machine configuration for TQ-Systems GmbH TQMLX2160A
#@MAINTAINER: Gregor Herburger <gregor.herburger@tq-group.com>
#
require conf/machine/include/tqmlx2160a.inc

# no default assignment here, since we overwrite defaults from qoriq-arm64.inc
SERIAL_CONSOLES = "115200;ttyS1"
SERIAL_CONSOLES_CHECK = "${SERIAL_CONSOLES}"
USE_VT = "0"

KERNEL_DEVICETREE ?= " \
    freescale/fsl-lx2160a-mblx2160a.dtb \
"

# Files are needed in IMAGE_BOOT_FILES because /boot is on an extra partition.
# If those files should be installed under /boot inside the rootfs, you can
# achieve this in your BSP layer adding them via
# IMAGE_INSTALL:append = " kernel-devicetree kernel-image-zimage"
#
IMAGE_BOOT_FILES = "\
    ${KERNEL_IMAGETYPE} \
    fsl-lx2160a-mblx2160a.dtb \
    mc_app/mc.itb;mc.itb \
    mc-utils/${MC_DPC};${MC_DPC} \
    mc-utils/${MC_DPL};${MC_DPL} \
"

# direct assignment to override empty default from meta-freescale
WKS_FILE = "tqmlx2160a.wks"

WKS_FILE_DEPENDS = "\
    dosfstools-native \
    mtools-native \
    parted-native \
    qoriq-atf \
    virtual/bootloader \
    virtual/kernel \
"

# Do not update fstab file when using wic images
WIC_CREATE_EXTRA_ARGS = "--no-fstab-update"

UBOOT_CONFIG ?= "tfa"

UBOOT_CONFIG[tfa] = "tqmlx2160a_mblx2160a_tfa_defconfig,wic"

# Reset Configuration Word for TQMLX2160A
rcw ?= "tqmlx2160a-mblx2160a/rcw_2200_750_2800_14_11_2"

#
# mc-utils example configuration for starter kit
# needs to be adjusted for custom boards / setups
#
MC_FOLDER ?= "${MACHINE}"
MC_DPC ?= "dpc-warn.dtb"
MC_DPL ?= "dpl-min.dtb"

BOOTTYPE ?= "\
    emmc \
    flexspi_nor \
    sd \
"

# Do not put Images into /boot of rootfs, install kernel-image if needed
RDEPENDS:${KERNEL_PACKAGE_NAME}-base = ""
