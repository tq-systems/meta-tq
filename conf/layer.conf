# We have a conf and classes directory, add to BBPATH
BBPATH .= ":${LAYERDIR}"

# We have recipes-* directories, add to BBFILES
BBFILES += "${LAYERDIR}/recipes-*/*/*.bb \
    ${LAYERDIR}/recipes-*/*/*.bbappend"

BBFILE_COLLECTIONS += "tq-layer"
BBFILE_PATTERN_tq-layer = "^${LAYERDIR}/"
BBFILE_PRIORITY_tq-layer = "9"

LAYERDEPENDS_tq-layer = "core"
LAYERSERIES_COMPAT_tq-layer = "hardknott"

# Add custom lisenses into the metadata
LICENSE_PATH += "${LAYERDIR}/custom-licenses"
# primary license for recipes using NXP EULA licensed binaries
# This is used by the fetching / unpacking support class (nxp-bin-unpack)
NXP_EULA_FILE = "${LAYERDIR}/custom-licenses/NXP-EULA-v24"

# The dynamic-layers directory hosts the extensions and layer specific
# modifications related to TQ Embedded products.
BBFILES_DYNAMIC += "\
   basler-imx8:${LAYERDIR}/dynamic-layers/basler-imx8/recipes-*/*/*.bbappend \
   basler-imx8:${LAYERDIR}/dynamic-layers/basler-imx8/recipes-*/*/*.bb \
   freescale-layer:${LAYERDIR}/dynamic-layers/freescale-layer/recipes-*/*/*.bbappend \
   freescale-layer:${LAYERDIR}/dynamic-layers/freescale-layer/recipes-*/*/*.bb \
   meta-ti:${LAYERDIR}/dynamic-layers/meta-ti/recipes-*/*/*.bbappend \
   meta-ti:${LAYERDIR}/dynamic-layers/meta-ti/recipes-*/*/*.bb \
   openembedded-layer:${LAYERDIR}/dynamic-layers/openembedded-layer/recipes-*/*/*.bbappend \
   openembedded-layer:${LAYERDIR}/dynamic-layers/openembedded-layer/recipes-*/*/*.bb \
"

#
# List of packages that provides targets which could also be found in vendor layers
#
BBMASK_FSL_LAYER = "\
    firmware-imx-tq \
"
BBMASK += "${@bb.utils.contains("BBFILE_COLLECTIONS", "freescale-layer", "${BBMASK_FSL_LAYER}", '', d)}"
# Do not add this .bbappend if meta-imx/meta-bsp is added (which has lower priority than meta-tq)
# This is just to update the SRCREV if basler-imx8 layer is used, which requires a more recent version than meta-freescale provides
BBMASK += "${@bb.utils.contains("BBFILE_COLLECTIONS", "fsl-bsp-release", "/meta-tq/dynamic-layers/basler-imx8/", '', d)}"

# this is the default location for TQ-Systems repos
# since this layer is intended to derive baseboards for TQ-Systems computing
# modules, define a variable which can be overwritten by machines, dependend
# layers or local.conf. Idea was borrowed from Stefan Lange <s.lange@gateware.de>
TQ_GIT_BASEURL ??= "git://github.com/tq-systems"
TQ_GIT_PROTOCOL ??= "https"
